000100*****
000200***** CLEANED             20/05/98
000300*****
000400 IDENTIFICATION DIVISION.
000500 PROGRAM-ID.                               PIAUTO.
000600 AUTHOR.                                   NGUYEN.
000700 DATE-WRITTEN.                             FEVRIER  1991.
000800 ENVIRONMENT DIVISION.
000900 DATA DIVISION.
001000 WORKING-STORAGE SECTION.
001100     COPY STSP00DA.
001200*UPDATED FOR TBEN BY NS 19/08/2004 Pb n° IDENT erroné
001203     COPY COMAUTO2.                                               150404NL
001205*END OF UPDATE BY NS
001206*
001300 01  MSTD-DATA.
001400     COPY ST0000DB.
001500*
001600     COPY ST0000DD.
001700*
001800     COPY STERREUR.
001900*
002000     COPY ZTBORCOL.
002100*
002200     EXEC SQL INCLUDE
002300              TBORCOL
002400     END-EXEC.
002500*
002600     EXEC SQL INCLUDE
002700              SQLCA
002800     END-EXEC.
002900*
003000 LINKAGE SECTION.
003100 01  DFHCOMMAREA.  COPY  COMAUTO  REPLACING
003200                      ==01  COMAUTO.== BY ====
003300                      ==COMAUTO== BY ==DFHCOMMAREA==.
003400*
003500     COPY COPCWA.
003600*
003700 PROCEDURE DIVISION.
003800     EXEC CICS HANDLE ABEND
003900               LABEL (90000-ERROR-ABEND)
004000     END-EXEC.
004100     INITIALIZE STDER-ERREUR.
004200     MOVE EIBDATE                       TO STDER-DATE.
004300     MOVE EIBTIME                       TO STDER-HEURE.
004400*
004500* *--------------------INIT HANDLE CONDITION CICS
004600*
004700     PERFORM 90000-CONDERR-INIT.
004800*
004900*----RECUPERER L'ABSTIME DE LA FONCTION
005000     EXEC CICS ASKTIME
005100               ABSTIME(STDER-ABSTIME-DEBUT)
005200     END-EXEC.
005300
005400*
005500*---IDENTIFIER LA SESSION CICS
005600     EXEC CICS ASSIGN APPLID (WS-SESS-ID)
005700     END-EXEC.
005800*
005900*---RECUPERER LA SIGNATURE CICS (CESN)
006000     EXEC CICS ASSIGN USERID (WS-USER-ID)
006100     END-EXEC.
006200*
006300     EXEC CICS ADDRESS CWA (ADDRESS OF CWAZONE)
006400     END-EXEC.
006500     MOVE  EIBDATE          TO WS-DQCPU.
006600     PERFORM  60000-TRANSF-DATES.
006700     MOVE  CWA-SIECLE       TO WS-DTCPU-S
006800                               WS-DQCPU-S
006900                               WS-DQTRT-S.
007000
007100*---RECUPERER LA DATE SYSTEME EN FORMAT DB2: (WS-DTCPU-DB2)
007200
007300     MOVE  '.'           TO   WS-DTCPU-DB2-POINT1
007400                              WS-DTCPU-DB2-POINT2.
007500     MOVE  WS-DTCPU-JJ   TO   WS-DTCPU-DB2-JJ.
007600     MOVE  WS-DTCPU-MM   TO   WS-DTCPU-DB2-MM.
007700     MOVE  WS-DTCPU-S    TO   WS-DTCPU-DB2-SS.
007800     MOVE  WS-DTCPU-AA   TO   WS-DTCPU-DB2-AA.
007900*
008000     INITIALIZE  DFHCOMMAREA.
008100*
008200     IF WS-USER-ID = WS-CICSUSER-NOM
008300        MOVE +1                         TO COM-AUTSIGN
008400        GO TO FIN
008500     END-IF.
008600     MOVE WS-USER-ID                    TO TCOLCUSER.
008700*
008800     PERFORM SELECT-TBORCOL THRU SELECT-TBORCOL-FIN.
008900*
009000     EVALUATE TRUE
009100         WHEN SQLCODE = 100
009200              MOVE +2                   TO COM-AUTSIGN
009300              GO TO  FIN
009400         WHEN SQLCODE < ZERO
009500              MOVE 'SELECT TBORCOL'     TO STDER-MESSAGE
009600              MOVE 'PIAUTO'             TO STDER-BOITE
009700              MOVE TCOLCUSER            TO ZCOLCUSER
009800              MOVE TCOLLBASEUSER        TO ZCOLLBASEUSER
009900              MOVE TCOLNOID             TO ZCOLNOID
010000              MOVE ZTBORCOL             TO STDER-DB2-CLE
010100              MOVE 'TBORCOL'            TO STDER-DB2-TABLE
010200              MOVE 'DB2 '               TO STDER-TYPE-ERREUR
010300              PERFORM 90000-ENREG-ERROR
010400              PERFORM 90000-TRAIT-ERROR
010500     END-EVALUATE.
010600*
010700     IF  EIBTRMID NOT = TCOLNTERM
010800         MOVE WS-DTCPU-DB2              TO TCOLDMOD
010900         MOVE EIBTRMID                  TO TCOLNTERM
011000         PERFORM UPDATE-TBORCOL THRU UPDATE-TBORCOL-FIN
011100         EVALUATE TRUE
011200             WHEN SQLCODE < ZERO
011300                  MOVE 'UPDATE TBORCOL' TO STDER-MESSAGE
011400                  MOVE 'PIAUTO'         TO STDER-BOITE
011500                  MOVE TCOLCUSER        TO ZCOLCUSER
011600                  MOVE TCOLLBASEUSER    TO ZCOLLBASEUSER
011700                  MOVE TCOLNOID         TO ZCOLNOID
011800                  MOVE ZTBORCOL         TO STDER-DB2-CLE
011900                  MOVE 'TBORCOL'        TO STDER-DB2-TABLE
012000                  MOVE 'DB2 '           TO STDER-TYPE-ERREUR
012100                  PERFORM 90000-ENREG-ERROR
012200                  PERFORM 90000-TRAIT-ERROR
012300         END-EVALUATE
012400     END-IF.
012500*
012600     MOVE TCOLNOID                      TO COM-AUTNOID.
012601*UPDATED FOR TBEN BY NS 19/08/2004 Pb n° IDENT erroné
012610*                                                                 150404NL
012620****  AJOUTS MODIFS PB NO IDENT ERRONE  ****                      150404NL
012630*                                                                 150404NL
012640     MOVE EIBTRMID                      TO WS-TS-TRMID.           150404NL
012650     MOVE WS-TS-AUTO-NOM                TO WS-TS-NOM.             150404NL
012660                                                                  150404NL
012670     EXEC CICS READQ   TS QUEUE  (WS-TS-ID)                       150404NL
012680                          INTO   (COMAUTO2)                       150404NL
012690                          LENGTH (LENGTH OF COMAUTO2)             150404NL
012691                          ITEM   (+1)                             150404NL
012692                          NOHANDLE                                150404NL
012693     END-EXEC.                                                    150404NL
012694                                                                  150404NL
012695     IF  EIBRESP  NOT EQUAL  DFHRESP(QIDERR)                      150404NL
012696         IF  COM-AUTNOID  NOT EQUAL  COM2-AUTNOID                 150404NL
012697             EXEC CICS DELETEQ TS QUEUE  (WS-TS-ID)               150404NL
012698                               NOHANDLE                           150404NL
012699             END-EXEC                                             150404NL
012700         END-IF                                                   150404NL
012701     END-IF.                                                      150404NL
012702*                                                                 150404NL
012703****  FIN AJOUTS MODIFS PB NO IDENT ERRONE  ****                  150404NL
012705*END OF UPDATE BY NS
012707*
012710*
012800* RECHERCHE SI IL EST SUIVI
012900*
013000     PERFORM VALID-SUIVI THRU VALID-SUIVI-FIN
013100*
013200* RECHERCHE S IL POSSEDE UNE DéLéGATION
013300* SI OUI, ET QU ELLE EST EN VIGUEUR, UN CERTAIN NOMBRE DE COMPE
013400* TENCE DU DELEGATAIRE SONT REMPLACEES PAR CELLES DU DELEGANT
013500*
013600     IF  TCOLNOIDELEG = ZERO
013700         GO TO ALIM-COMAUTO
013800     END-IF.
013900
014000     MOVE TCOLDDELEGDEB                 TO WS-DB2-DATE.
014100     PERFORM 60000-MEF-DATE-AFFICH.
014200     IF  WS-AFFICH-DATE > WS-DTCPU
014300         GO TO  ALIM-COMAUTO
014400     END-IF.
014500
014600*  LA FIN DE DELEGATION SE FAIT EN BATCH OU PAR LA FONCTION DELG
014700*  POUR LA BASCULE DES LISTES
014800
014900*  EN FIN DE DELEGATION, REMISE A ZERO DES ZONES DELEGATION ET
015000*  CONFIGURATION
015100*
015200*
015300     MOVE TCOLDDELEGFIN                 TO WS-DB2-DATE.
015400     PERFORM 60000-MEF-DATE-AFFICH.
015500     IF  WS-AFFICH-DATE < WS-DTCPU
015600         MOVE    SPACES                 TO TCOLCTYPDELEG
015700         MOVE WS-DTCPU-DB2              TO TCOLDMOD
015800         PERFORM UPDATE-TBORCOL-DELEGANT
015900         THRU UPDATE-TBORCOL-DELEGANT-FIN
016000         EVALUATE TRUE
016100             WHEN SQLCODE < ZERO
016200                  MOVE 'UPDATE DELEGAN' TO STDER-MESSAGE
016300                  MOVE 'PIAUTO'         TO STDER-BOITE
016400                  MOVE TCOLCUSER        TO ZCOLCUSER
016500                  MOVE TCOLLBASEUSER    TO ZCOLLBASEUSER
016600                  MOVE TCOLNOID         TO ZCOLNOID
016700                  MOVE ZTBORCOL         TO STDER-DB2-CLE
016800                  MOVE 'TBORCOL'        TO STDER-DB2-TABLE
016900                  MOVE 'DB2 '           TO STDER-TYPE-ERREUR
017000                  PERFORM 90000-ENREG-ERROR
017100                  PERFORM 90000-TRAIT-ERROR
017200         END-EVALUATE
017300         MOVE    '0'                    TO TCOLCEFFZCONF
017400         MOVE    ZERO                   TO TCOLNCONFAGC
017500         MOVE    ZERO                   TO TCOLNCONFSOC
017600         MOVE    ZERO                   TO TCOLNOIDELEG
017700         MOVE    '01.01.0001'           TO TCOLDDELEGDEB
017800         MOVE    '01.01.0001'           TO TCOLDDELEGFIN
017900         MOVE    SPACES                 TO TCOLCTYPDELEG
018000         MOVE    SPACES                 TO TCOLCPOLEACTIV
018100         MOVE    SPACES                 TO TCOLCFONCSOMM
018200         MOVE WS-DTCPU-DB2              TO TCOLDMOD
018300         PERFORM UPDATE-TBORCOL-FINDELEG
018400         THRU UPDATE-TBORCOL-FINDELEG-FIN
018500         EVALUATE TRUE
018600             WHEN SQLCODE < ZERO
018700                  MOVE 'UPDATE TBORCO3' TO STDER-MESSAGE
018800                  MOVE 'PIAUTO'         TO STDER-BOITE
018900                  MOVE TCOLCUSER        TO ZCOLCUSER
019000                  MOVE TCOLLBASEUSER    TO ZCOLLBASEUSER
019100                  MOVE TCOLNOID         TO ZCOLNOID
019200                  MOVE ZTBORCOL         TO STDER-DB2-CLE
019300                  MOVE 'TBORCOL'        TO STDER-DB2-TABLE
019400                  MOVE 'DB2 '           TO STDER-TYPE-ERREUR
019500                  PERFORM 90000-ENREG-ERROR
019600                  PERFORM 90000-TRAIT-ERROR
019700         END-EVALUATE
019800         GO TO  ALIM-COMAUTO
019900     END-IF.
020000
020100     PERFORM  ALIM-COMAUTO.
020200
020300
020400* AU 1 ER JOUR DE LA MISE EN OEUVRE DE LA DELEGATION, LA 1èRE FOI
020500* QUE LE DéLéGATAIRE ASSURE SES NOUVELLES FONCTIONS, ON EFFACE SA
020600* PROPRE CONFIGURATION POUR QU'IL SE RECONFIGURE EN FONCTION DE
020700* NOUVELLES COMPéTENCES PROVISOIRES
020800
020900     IF  TCOLCEFFZCONF = '0' OR SPACES
021000         MOVE    '1'                    TO TCOLCEFFZCONF
021100         MOVE    ZERO                   TO TCOLNCONFAGC
021200         MOVE    ZERO                   TO TCOLNCONFSOC
021300         MOVE    SPACES                 TO TCOLCPOLEACTIV
021400         MOVE    SPACES                 TO TCOLCFONCSOMM
021500         MOVE    WS-DTCPU-DB2           TO TCOLDMOD
021600         PERFORM UPDATE-TBORCOL-EFFZCONF
021700         THRU UPDATE-TBORCOL-EFFZCONF-FIN
021800         EVALUATE TRUE
021900             WHEN SQLCODE < ZERO
022000                  MOVE 'UPDATE TBORCOL' TO STDER-MESSAGE
022100                  MOVE 'PIAUTO'         TO STDER-BOITE
022200                  MOVE TCOLCUSER        TO ZCOLCUSER
022300                  MOVE TCOLLBASEUSER    TO ZCOLLBASEUSER
022400                  MOVE TCOLNOID         TO ZCOLNOID
022500                  MOVE ZTBORCOL         TO STDER-DB2-CLE
022600                  MOVE 'TBORCOL'        TO STDER-DB2-TABLE
022700                  MOVE 'DB2 '           TO STDER-TYPE-ERREUR
022800                  PERFORM 90000-ENREG-ERROR
022900                  PERFORM 90000-TRAIT-ERROR
023000         END-EVALUATE
023100     END-IF.
023200*
023300     MOVE TCOLNOIDELEG                  TO TCOLNOID.
023400*
023500*  EN CAS DE DELEGATION, LE DELEGATAIRE (CE QUI REMPLACE) PREND
023600*  CERTAINES COMPETENCES DU DELEGANT (CELUI QUI EST REMPLACE)
023700*  QUAND CELUI-CI A UN NIVEAU HIERARCHIQUE SUPERIEUR OU EGAL
023800*  AU SIEN SINON LE DELEGATAIRE GARDENT SES COMPETENCES.
023900
024000*
024100     PERFORM SELECT-TBORCOL2 THRU SELECT-TBORCOL2-FIN.
024200*
024300     EVALUATE TRUE
024400         WHEN SQLCODE = 100
024500              MOVE +3                   TO  COM-AUTSIGN
024600              GO TO FIN
024700         WHEN SQLCODE < ZERO
024800              MOVE 'SELECT TBORCOL'     TO STDER-MESSAGE
024900              MOVE 'PIAUTO'             TO STDER-BOITE
025000              MOVE SPACE                TO ZCOLCUSER
025100              MOVE SPACE                TO ZCOLLBASEUSER
025200              MOVE TCOLNOID             TO ZCOLNOID
025300              MOVE ZTBORCOL             TO STDER-DB2-CLE
025400              MOVE 'TBORCOL'            TO STDER-DB2-TABLE
025500              MOVE 'DB2 '               TO STDER-TYPE-ERREUR
025600              PERFORM 90000-ENREG-ERROR
025700              PERFORM 90000-TRAIT-ERROR
025800     END-EVALUATE.
025900
026000     IF  COM-AUTNIVEAUH NOT < TCOLCNIVEAUHIER
026100         GO  TO   FIN
026200     END-IF.
026300
026400*
026500 ALIM-COMAUTO.
026600     MOVE TCOLLINITUSER                 TO COM-AUTLINITUSER.
026700     MOVE TCOLCSOCAPPART                TO COM-AUTCSOCAPPART.
026800     MOVE TCOLCNIVEAUHIER               TO COM-AUTNIVEAUH.
026900     MOVE TCOLCFONCSOMM                 TO COM-AUTCFONCSOMM.
027000     MOVE TCOLLAFFICHECH                TO COM-AUTLAFFICHECH.
027100     MOVE TCOLNOIDELEG                  TO COM-AUTNOIDELEG.
027200     MOVE TCOLDDELEGDEB                 TO COM-AUTDDELEGDEB.
027300     MOVE TCOLDDELEGFIN                 TO COM-AUTDDELEGFIN.
027400     MOVE TCOLCTYPDELEG                 TO COM-AUTCTYPDELEG.
027500     MOVE TCOLCCOMPTER                  TO COM-AUTCOMPTER.
027600     MOVE TCOLCCOMPZON                  TO COM-AUTCOMPZON.
027700     MOVE TCOLNAFFECAGC1                TO COM-AUTAFFECAGC1.
027800     MOVE TCOLNAFFECAGC2                TO COM-AUTAFFECAGC2.
027900     MOVE TCOLNAFFECAGC3                TO COM-AUTAFFECAGC3.
028000     MOVE TCOLNDEPAAGC1                 TO COM-AUTDEPAAGC1.
028100     MOVE TCOLNDEPAAGC2                 TO COM-AUTDEPAAGC2.
028200     MOVE TCOLNDEPAAGC3                 TO COM-AUTDEPAAGC3.
028300     MOVE TCOLNDEPAAGC4                 TO COM-AUTDEPAAGC4.
028400     MOVE TCOLNDEPAAGC5                 TO COM-AUTDEPAAGC5.
028500     MOVE TCOLNDEPAAGC6                 TO COM-AUTDEPAAGC6.
028600     MOVE TCOLNDEPAAGC7                 TO COM-AUTDEPAAGC7.
028700     MOVE TCOLNDEPAAGC8                 TO COM-AUTDEPAAGC8.
028800     MOVE TCOLNCONFAGC                  TO COM-AUTNCONFAGC.
028900     MOVE TCOLCSTAT                     TO COM-AUTSTAT.
029000     MOVE TCOLCCLAST                    TO COM-AUTCLAST.
029100     MOVE TCOLCCOMPSOC1                 TO COM-AUTCOMPSOC1.
029200     MOVE TCOLCCOMPSOC2                 TO COM-AUTCOMPSOC2.
029300     MOVE TCOLCCOMPSOC3                 TO COM-AUTCOMPSOC3.
029400     MOVE TCOLCCOMPSOC4                 TO COM-AUTCOMPSOC4.
029500     MOVE TCOLCCOMPSOC5                 TO COM-AUTCOMPSOC5.
029600     MOVE TCOLCCOMPSOC6                 TO COM-AUTCOMPSOC6.
029700     MOVE TCOLCCOMPSOC7                 TO COM-AUTCOMPSOC7.
029800     MOVE TCOLCCOMPSOC8                 TO COM-AUTCOMPSOC8.
029900     MOVE TCOLCCOMPSOC9                 TO COM-AUTCOMPSOC9.
030000     MOVE TCOLCCOMPSOC10                TO COM-AUTCOMPSOC10.
030100     MOVE TCOLCCOMPSOC11                TO COM-AUTCOMPSOC11.
030200     MOVE TCOLNCONFSOC                  TO COM-AUTNCONFSOC.
030300     MOVE TCOLCVISITOTALE               TO COM-AUTCVISITOTALE.
030400     MOVE TCOLCPOLEACTIV                TO COM-AUTPOLEACTIV.
030500     MOVE TCOLCBAC                      TO COM-AUTCBAC.
030600     MOVE TCOLNIMP                      TO COM-AUTNIMP.           IMPR
030700     MOVE TCOLCOCTROI                   TO COM-AUTCOCTROI.
030800     MOVE TCOLCFINCT                    TO COM-AUTCFINCT.
030900     MOVE TCOLCAPV                      TO COM-AUTAPRVT.
031000     MOVE TCOLCSSR                      TO COM-AUTSSR.
031100     MOVE TCOLCCTX                      TO COM-AUTCTX.
031200     MOVE TCOLCCTRGAL                   TO COM-AUTCTRGAL.
031300     MOVE TCOLCCOMPTA                   TO COM-AUTCOMPTA.
031400     MOVE TCOLCCOMPTARIB                TO COM-AUTCOMPTARIB.
031500     MOVE TCOLCHELP                     TO COM-AUTHELP.
031600     MOVE TCOLCSECURITE                 TO COM-AUTSECURITE.
031700     MOVE TCOLCCOMMERC                  TO COM-AUTCOMMERC.
031800     MOVE TCOLCCPTCLI                   TO COM-AUTCPTCLI.
031900*
032000 FIN.
032100     IF  EIBTRNID  NOT  =  'ZVI1'
032200                      AND  'ZVI2'
032300                      AND  'ZVA1'
032400                      AND  'ZVA2'
032500                      AND  'ZVC1'
032600                      AND  'ZVC2'
032700                      AND  'ZVO1'
032800                      AND  'ZVO2'
032900         EXEC CICS SYNCPOINT
033000         END-EXEC
033100     END-IF
033200*
033300     EXEC CICS RETURN
033400     END-EXEC.
033500*
033600     GOBACK.
033700*
033800* *------------------------------------------------------------*
033900* *      SELECT D'UNE LIGNE DE LA TABLE TBORCOL PAR CODE USER
034000* *------------------------------------------------------------*
034100 SELECT-TBORCOL.
034200     EXEC SQL
034300          SELECT   TCOLCUSER
034400                ,  TCOLNTERM
034500                ,  TCOLNOID
034600                ,  TCOLLINITUSER
034700                ,  TCOLCSOCAPPART
034800                ,  TCOLCNIVEAUHIER
034900                ,  TCOLCFONCSOMM
035000                ,  TCOLLAFFICHECH
035100                ,  TCOLCEFFZCONF
035200                ,  TCOLNOIDELEG
035300                ,  TCOLDDELEGDEB
035400                ,  TCOLDDELEGFIN
035500                ,  TCOLCTYPDELEG
035600                ,  TCOLDSURVDEB
035700                ,  TCOLDSURVFIN
035800                ,  TCOLCSURVDOS
035900                ,  TCOLCSURVECOUTE
036000                ,  TCOLCCOMPTER
036100                ,  TCOLCCOMPZON
036200                ,  TCOLNAFFECAGC1
036300                ,  TCOLNAFFECAGC2
036400                ,  TCOLNAFFECAGC3
036500                ,  TCOLNDEPAAGC1
036600                ,  TCOLNDEPAAGC2
036700                ,  TCOLNDEPAAGC3
036800                ,  TCOLNDEPAAGC4
036900                ,  TCOLNDEPAAGC5
037000                ,  TCOLNDEPAAGC6
037100                ,  TCOLNDEPAAGC7
037200                ,  TCOLNDEPAAGC8
037300                ,  TCOLNCONFAGC
037400                ,  TCOLCSTAT
037500                ,  TCOLCCLAST
037600                ,  TCOLCCOMPSOC1
037700                ,  TCOLCCOMPSOC2
037800                ,  TCOLCCOMPSOC3
037900                ,  TCOLCCOMPSOC4
038000                ,  TCOLCCOMPSOC5
038100                ,  TCOLCCOMPSOC6
038200                ,  TCOLCCOMPSOC7
038300                ,  TCOLCCOMPSOC8
038400                ,  TCOLCCOMPSOC9
038500                ,  TCOLCCOMPSOC10
038600                ,  TCOLCCOMPSOC11
038700                ,  TCOLNCONFSOC
038800                ,  TCOLCVISITOTALE
038900                ,  TCOLCPOLEACTIV
039000                ,  TCOLCBAC
039100                ,  TCOLCCOMMERC
039200                ,  TCOLCOCTROI
039300                ,  TCOLCFINCT
039400                ,  TCOLCAPV
039500                ,  TCOLCSSR
039600                ,  TCOLCCTX
039700                ,  TCOLCCTRGAL
039800                ,  TCOLCCPTCLI
039900                ,  TCOLCCOMPTA
040000                ,  TCOLCCOMPTARIB
040100                ,  TCOLCHELP
040200                ,  TCOLCSECURITE
040300                ,  TCOLNIMP                                       IMPR
040400           INTO   :TCOLCUSER
040500                , :TCOLNTERM
040600                , :TCOLNOID
040700                , :TCOLLINITUSER
040800                , :TCOLCSOCAPPART
040900                , :TCOLCNIVEAUHIER
041000                , :TCOLCFONCSOMM
041100                , :TCOLLAFFICHECH
041200                , :TCOLCEFFZCONF
041300                , :TCOLNOIDELEG
041400                , :TCOLDDELEGDEB
041500                , :TCOLDDELEGFIN
041600                , :TCOLCTYPDELEG
041700                , :TCOLDSURVDEB
041800                , :TCOLDSURVFIN
041900                , :TCOLCSURVDOS
042000                , :TCOLCSURVECOUTE
042100                , :TCOLCCOMPTER
042200                , :TCOLCCOMPZON
042300                , :TCOLNAFFECAGC1
042400                , :TCOLNAFFECAGC2
042500                , :TCOLNAFFECAGC3
042600                , :TCOLNDEPAAGC1
042700                , :TCOLNDEPAAGC2
042800                , :TCOLNDEPAAGC3
042900                , :TCOLNDEPAAGC4
043000                , :TCOLNDEPAAGC5
043100                , :TCOLNDEPAAGC6
043200                , :TCOLNDEPAAGC7
043300                , :TCOLNDEPAAGC8
043400                , :TCOLNCONFAGC
043500                , :TCOLCSTAT
043600                , :TCOLCCLAST
043700                , :TCOLCCOMPSOC1
043800                , :TCOLCCOMPSOC2
043900                , :TCOLCCOMPSOC3
044000                , :TCOLCCOMPSOC4
044100                , :TCOLCCOMPSOC5
044200                , :TCOLCCOMPSOC6
044300                , :TCOLCCOMPSOC7
044400                , :TCOLCCOMPSOC8
044500                , :TCOLCCOMPSOC9
044600                , :TCOLCCOMPSOC10
044700                , :TCOLCCOMPSOC11
044800                , :TCOLNCONFSOC
044900                , :TCOLCVISITOTALE
045000                , :TCOLCPOLEACTIV
045100                , :TCOLCBAC
045200                , :TCOLCCOMMERC
045300                , :TCOLCOCTROI
045400                , :TCOLCFINCT
045500                , :TCOLCAPV
045600                , :TCOLCSSR
045700                , :TCOLCCTX
045800                , :TCOLCCTRGAL
045900                , :TCOLCCPTCLI
046000                , :TCOLCCOMPTA
046100                , :TCOLCCOMPTARIB
046200                , :TCOLCHELP
046300                , :TCOLCSECURITE
046400                , :TCOLNIMP                                       IMPR
046500          FROM  TBORCOL
046600          WHERE TCOLCUSER     = :TCOLCUSER
046700     END-EXEC.
046800*
046900 SELECT-TBORCOL-FIN.
047000     EXIT.
047100*
047200* *------------------------------------------------------------*
047300* *           UPDATE DE LIGNE DE LA TABLE TBORCOL
047400* *------------------------------------------------------------*
047500 UPDATE-TBORCOL.
047600     EXEC SQL
047700          UPDATE TBORCOL
047800             SET TCOLDMOD             = :TCOLDMOD
047900               , TCOLNTERM            = :TCOLNTERM
048000          WHERE TCOLCUSER     = :TCOLCUSER
048100     END-EXEC.
048200*
048300 UPDATE-TBORCOL-FIN.
048400     EXIT.
048500*
048600* *------------------------------------------------------------*
048700* *           UPDATE DE LIGNE DE LA TABLE TBORCOL DELEGANT
048800* *------------------------------------------------------------*
048900 UPDATE-TBORCOL-DELEGANT.
049000     EXEC SQL
049100          UPDATE TBORCOL
049200             SET TCOLDMOD             = :TCOLDMOD
049300               , TCOLCTYPDELEG        = :TCOLCTYPDELEG
049400          WHERE TCOLNOID   = :TCOLNOIDELEG
049500     END-EXEC.
049600
049700 UPDATE-TBORCOL-DELEGANT-FIN.
049800     EXIT.
049900*
050000* *------------------------------------------------------------*
050100* *           UPDATE TBORCOL RAZ CONF
050200* *------------------------------------------------------------*
050300 UPDATE-TBORCOL-EFFZCONF.
050400     EXEC SQL
050500          UPDATE TBORCOL
050600             SET TCOLDMOD             = :TCOLDMOD
050700               , TCOLCEFFZCONF        = :TCOLCEFFZCONF
050800               , TCOLNCONFAGC         = :TCOLNCONFAGC
050900               , TCOLNCONFSOC         = :TCOLNCONFSOC
051000               , TCOLCPOLEACTIV       = :TCOLCPOLEACTIV
051100               , TCOLCFONCSOMM        = :TCOLCFONCSOMM
051200          WHERE TCOLCUSER     = :TCOLCUSER
051300     END-EXEC.
051400*
051500 UPDATE-TBORCOL-EFFZCONF-FIN.
051600     EXIT.
051700*
051800* *------------------------------------------------------------*
051900* *           UPDATE TBORCOL ZONE CONF FIN DELEGATION
052000* *------------------------------------------------------------*
052100 UPDATE-TBORCOL-FINDELEG.
052200     EXEC SQL
052300          UPDATE TBORCOL
052400             SET TCOLDMOD             = :TCOLDMOD
052500               , TCOLCEFFZCONF        = :TCOLCEFFZCONF
052600               , TCOLNCONFAGC         = :TCOLNCONFAGC
052700               , TCOLNCONFSOC         = :TCOLNCONFSOC
052800               , TCOLCPOLEACTIV       = :TCOLCPOLEACTIV
052900               , TCOLCFONCSOMM        = :TCOLCFONCSOMM
053000               , TCOLNOIDELEG         = :TCOLNOIDELEG
053100               , TCOLDDELEGDEB        = :TCOLDDELEGDEB
053200               , TCOLDDELEGFIN        = :TCOLDDELEGFIN
053300               , TCOLCTYPDELEG        = :TCOLCTYPDELEG
053400          WHERE TCOLCUSER     = :TCOLCUSER
053500     END-EXEC.
053600*
053700 UPDATE-TBORCOL-FINDELEG-FIN.
053800     EXIT.
053900*
054000* *------------------------------------------------------------*
054100* *           UPDATE TBORCOL ZONE SUIVI FIN SUIVI
054200* *------------------------------------------------------------*
054300 UPDATE-TBORCOL-FINSUIVI.
054400     EXEC SQL
054500          UPDATE TBORCOL
054600             SET TCOLDMOD             = :TCOLDMOD
054700               , TCOLCSURVDOS         = :TCOLCSURVDOS
054800               , TCOLCSURVECOUTE      = :TCOLCSURVECOUTE
054900               , TCOLDSURVDEB         = :TCOLDSURVDEB
055000               , TCOLDSURVFIN         = :TCOLDSURVFIN
055100          WHERE TCOLCUSER     = :TCOLCUSER
055200     END-EXEC.
055300*
055400 UPDATE-TBORCOL-FINSUIVI-FIN.
055500     EXIT.
055600*
055700* *------------------------------------------------------------*
055800* *      SELECT D'UNE LIGNE DE LA TABLE TBORCOL PAR NOID
055900* *------------------------------------------------------------*
056000 SELECT-TBORCOL2.
056100     EXEC SQL
056200          SELECT   TCOLCSOCAPPART
056300                ,  TCOLCNIVEAUHIER
056400                ,  TCOLCCOMPTER
056500                ,  TCOLCCOMPZON
056600                ,  TCOLNAFFECAGC1
056700                ,  TCOLNAFFECAGC2
056800                ,  TCOLNAFFECAGC3
056900                ,  TCOLNDEPAAGC1
057000                ,  TCOLNDEPAAGC2
057100                ,  TCOLNDEPAAGC3
057200                ,  TCOLNDEPAAGC4
057300                ,  TCOLNDEPAAGC5
057400                ,  TCOLNDEPAAGC6
057500                ,  TCOLNDEPAAGC7
057600                ,  TCOLNDEPAAGC8
057700                ,  TCOLCSTAT
057800                ,  TCOLCCLAST
057900                ,  TCOLCCOMPSOC1
058000                ,  TCOLCCOMPSOC2
058100                ,  TCOLCCOMPSOC3
058200                ,  TCOLCCOMPSOC4
058300                ,  TCOLCCOMPSOC5
058400                ,  TCOLCCOMPSOC6
058500                ,  TCOLCCOMPSOC7
058600                ,  TCOLCCOMPSOC8
058700                ,  TCOLCCOMPSOC9
058800                ,  TCOLCCOMPSOC10
058900                ,  TCOLCCOMPSOC11
059000                ,  TCOLCVISITOTALE
059100                ,  TCOLCBAC
059200                ,  TCOLCCOMMERC
059300                ,  TCOLCOCTROI
059400                ,  TCOLCFINCT
059500                ,  TCOLCAPV
059600                ,  TCOLCSSR
059700                ,  TCOLCCTX
059800                ,  TCOLCCTRGAL
059900                ,  TCOLCCPTCLI
060000                ,  TCOLCCOMPTA
060100                ,  TCOLCCOMPTARIB
060200                ,  TCOLCHELP
060300                ,  TCOLCSECURITE
060400                ,  TCOLNIMP                                       IMPR
060500           INTO   :TCOLCSOCAPPART
060600                , :TCOLCNIVEAUHIER
060700                , :TCOLCCOMPTER
060800                , :TCOLCCOMPZON
060900                , :TCOLNAFFECAGC1
061000                , :TCOLNAFFECAGC2
061100                , :TCOLNAFFECAGC3
061200                , :TCOLNDEPAAGC1
061300                , :TCOLNDEPAAGC2
061400                , :TCOLNDEPAAGC3
061500                , :TCOLNDEPAAGC4
061600                , :TCOLNDEPAAGC5
061700                , :TCOLNDEPAAGC6
061800                , :TCOLNDEPAAGC7
061900                , :TCOLNDEPAAGC8
062000                , :TCOLCSTAT
062100                , :TCOLCCLAST
062200                , :TCOLCCOMPSOC1
062300                , :TCOLCCOMPSOC2
062400                , :TCOLCCOMPSOC3
062500                , :TCOLCCOMPSOC4
062600                , :TCOLCCOMPSOC5
062700                , :TCOLCCOMPSOC6
062800                , :TCOLCCOMPSOC7
062900                , :TCOLCCOMPSOC8
063000                , :TCOLCCOMPSOC9
063100                , :TCOLCCOMPSOC10
063200                , :TCOLCCOMPSOC11
063300                , :TCOLCVISITOTALE
063400                , :TCOLCBAC
063500                , :TCOLCCOMMERC
063600                , :TCOLCOCTROI
063700                , :TCOLCFINCT
063800                , :TCOLCAPV
063900                , :TCOLCSSR
064000                , :TCOLCCTX
064100                , :TCOLCCTRGAL
064200                , :TCOLCCPTCLI
064300                , :TCOLCCOMPTA
064400                , :TCOLCCOMPTARIB
064500                , :TCOLCHELP
064600                , :TCOLCSECURITE
064700                , :TCOLNIMP                                       IMPR
064800          FROM  TBORCOL
064900          WHERE TCOLNOID      = :TCOLNOID
065000     END-EXEC.
065100*
065200 SELECT-TBORCOL2-FIN.
065300     EXIT.
065400*
065500*
065600 VALID-SUIVI.
065700*
065800     MOVE TCOLDSURVFIN                  TO  WS-DB2-DATE.
065900     PERFORM 60000-MEF-DATE-AFFICH.
066000     IF   WS-AFFICH-DATE  <  WS-DTCPU
066100     AND  WS-AFFICH-DATE  NOT = ZERO
066200         MOVE SPACES                    TO    TCOLCSURVDOS
066300         MOVE SPACES                    TO    TCOLCSURVECOUTE
066400         MOVE '01.01.0001'              TO    TCOLDSURVDEB
066500         MOVE '01.01.0001'              TO    TCOLDSURVFIN
066600         MOVE WS-DTCPU-DB2              TO TCOLDMOD
066700         PERFORM UPDATE-TBORCOL-FINSUIVI
066800           THRU  UPDATE-TBORCOL-FINSUIVI-FIN
066900         EVALUATE TRUE
067000             WHEN SQLCODE < ZERO
067100                  MOVE 'UPDATE TBORCOL' TO STDER-MESSAGE
067200                  MOVE 'PIAUTO'         TO STDER-BOITE
067300                  MOVE TCOLCUSER        TO ZCOLCUSER
067400                  MOVE TCOLLBASEUSER    TO ZCOLLBASEUSER
067500                  MOVE TCOLNOID         TO ZCOLNOID
067600                  MOVE ZTBORCOL         TO STDER-DB2-CLE
067700                  MOVE 'TBORCOL'        TO STDER-DB2-TABLE
067800                  MOVE 'DB2 '           TO STDER-TYPE-ERREUR
067900                  PERFORM 90000-ENREG-ERROR
068000                  PERFORM 90000-TRAIT-ERROR
068100         END-EVALUATE
068200         GO TO  VALID-SUIVI-FIN
068300     END-IF.
068400
068500     IF   TCOLCSURVDOS = SPACES OR LOW-VALUE
068600          MOVE 'NNNNNNNNNN'             TO COM-AUTCSURVDOS
068700     ELSE
068800          MOVE TCOLCSURVDOS             TO COM-AUTCSURVDOS
068900     END-IF.
069000
069100     IF   TCOLCSURVECOUTE = SPACES OR LOW-VALUE
069200          MOVE 'NNNNNNNNNN'             TO COM-AUTCSURVECOUTES
069300     ELSE
069400          MOVE TCOLCSURVECOUTE          TO COM-AUTCSURVECOUTES
069500     END-IF.
069600
069700
069800     MOVE TCOLDSURVDEB                  TO  WS-DB2-DATE.
069900     PERFORM 60000-MEF-DATE-AFFICH.
070000     IF   WS-AFFICH-DATE  >  WS-DTCPU
070100         MOVE 'NNNNNNNNNN'              TO COM-AUTCSURVDOS
070200         MOVE 'NNNNNNNNNN'              TO COM-AUTCSURVECOUTES
070300     END-IF.
070400*
070500 VALID-SUIVI-FIN.
070600     EXIT.
070700*
070800*
070900     COPY ST6000PB.
071000*
071100     COPY ST9000TR.
071200*
071300 90000-EVALUATE-TYPE-ERREUR SECTION.
071400* *----------------- EVALUATION DU TYPE D'ERREUR ET
071500* *----------------- CHARGEMENT DES CHAMPS SIGNIFICATIFS
071600     IF  STDER-ABSTIME-FIN = ZERO
071700         EXEC CICS ASKTIME
071800                   ABSTIME(STDER-ABSTIME-FIN)
071900         END-EXEC
072000     END-IF.
072100*
072200     EVALUATE  STDER-TYPE-ERREUR
072300         WHEN  'DB2'
072400               MOVE SQLCODE         TO  WS-SQLCODEX
072500                                        STDER-DB2-SQLCODE
072600               MOVE SQLCA           TO  STDER-DB2-SQLCA
072700               IF   SQLCODE > -900  AND SQLCODE NOT = -818
072800                                    AND SQLCODE NOT = -805
072900                    EXEC CICS
073000                         DUMP TASK DUMPCODE (WS-SQLCODEX)
073100                    END-EXEC
073200                    MOVE 'O'        TO  STDER-DUMP
073300               END-IF
073400     END-EVALUATE.
073500*
073600 90000-SPECIF-ERROR SECTION.
073700     IF  STDER-DUMP NOT = 'O'
073800         EXEC CICS ASKTIME
073900                   ABSTIME(STDER-ABSTIME-FIN)
074000         END-EXEC
074100     END-IF.
074200*
074300     MOVE 'PIAUTO'                      TO STDER-NOM-PROG.
074400*
074500 90000-TRAIT-ERROR SECTION.
074600     MOVE -1                            TO COM-AUTSIGN.
074700*
074800     EXEC CICS SYNCPOINT
074900     END-EXEC.
075000
075100     EXEC CICS RETURN
075200     END-EXEC.
075300
